Class Meta

	Attribute VB_Creatable = True
    Attribute VB_PredeclaredId = True
    Attribute VB_Exposed = True
    /*
    The Meta class provides a consistent interface for meta data
    on Types.
    
    On initialisation Metat derives the metadata but does not store
    the input cargo.
    
    The facility is provided to store the cargo
    The Meta class **does not** try to resolve if arrays are forwarded  ParamArrays

    The Meta class **does not** store a reference to ipCargo
    */

    Option Explicit

    Private Const MY_TYPENAME                   As String = "Meta"
    Private Const FIRST_RANK                    As Long = 1

    Public Enum CargoOp
    
        DoNotLoadIt = 0
        LoadIt = 1
        
    End Enum
    
    Private Type Properties
    
        ' Meta allows for ipCargo to be stored but does not automatically
        ' store data on in
        Cargo                                   As Variant
        Ranks                                   As Collection
        ' The number of elements in ipCargo
        Size                                    As Long
        HoldsCargo                              As Boolean
        IsArray                                 As boolean
        GroupOrd                                As TypeOrd
        TypeOrd                                 As TypeOrd
        
    End Type

    Private p                                   As Properties

    '@DefaultMember
    Public Function Deb(ByRef ipCargo As Variant, Optional ByVal ipCargoOp As CargoOp = DoNotLoadIt) As Meta
        With New Meta
            Return .ConstructInstance(ipCargo, ipCargoOp)
        End With
    End Function

    Friend Function ConstructInstance(ByRef ipCargo As Variant, ByVal ipCargoOp As CargoOp) As Meta
    
        SetupMetaData ipCargo, ipCargoOp
        Return Me
        
    End Function
    
    Public Sub SetupMetaData(ByRef ipCargo As Variant, ByVal ipCargoOp As CargoOp)
        
        Set p.Ranks = New Collection
        p.Size = 1
        p.IsArray = False
        p.GroupOrd = TypeOrd.idNoType
        p.TypeOrd = enums.Types.TypeNameToOrd(TypeInfo.BaseType(ipCargo))
        
        Select Case True
        
            Case VBA.IsArray(ipCargo)
            
                p.IsArray = True
                
                Dim myRanksCount As Long = ArrayInfo.Ranks(ipCargo)
                If myRanksCount > 0 Then
                
                    Dim myRank As Long
                    For myRank = 1 To myRanksCount
                        p.Ranks.Add Rank.Deb(LBound(ipCargo, myRank), UBound(ipCargo, myRank))
                        p.Size *= p.Ranks(myRank).count
                    Next
                    
                Else
                    
                    p.Ranks.Add Rank.Deb(0, -1)
                    p.Size = -1
        
                End If
                
                Select Case myRanksCount
                    Case 0: p.GroupOrd = TypeOrd.idArray
                    Case 1: p.GroupOrd = TypeOrd.idListArray
                    Case 2: p.GroupOrd = TypeOrd.idTableArray
                    Case Else: p.GroupOrd = TypeOrd.idMathArray
                    
                End Select
                
                
            Case TypeInfo.IsItemByKey(ipCargo)
            
                p.Ranks.Add Rank.Deb(0, CLng(ipCargo.Count - 1))
                p.GroupOrd = TypeOrd.idItemByKey
                p.Size = ipCargo.count
                
                
            Case TypeInfo.IsItemByToArray(ipCargo)
            
                p.Ranks.Add Rank.Deb(0, CLng(ipCargo.count - 1))
                p.GroupOrd = TypeOrd.idItemByToArray
                p.Size = ipCargo.count
                
                
            Case TypeInfo.IsItemByIndex0(ipCargo)
            
                p.Ranks.Add Rank.Deb(0, CLng(ipCargo.count - 1))
                p.GroupOrd = TypeOrd.idItemByIndex0
                p.Size = ipCargo.count
                

            Case TypeInfo.IsItemByIndex1(ipCargo)
                
                p.Ranks.Add Rank.Deb(1, CLng(ipCargo.count))
                p.GroupOrd = TypeOrd.idItemByIndex1
                p.Size = ipCargo.count
                
                
            Case TypeInfo.IsString(ipCargo)
            
                If VBA.Len(ipCargo) > 0 Then
                    p.Ranks.Add Rank.Deb(1, CLng(VBA.Len(ipCargo)))
                Else
                    p.Ranks.Add Rank.Deb(0, 0)
                End If
                p.GroupOrd = TypeOrd.idString
                p.Size = VBA.Len(ipCargo)
                
                
            Case TypeInfo.IsNumber(ipCargo)
            
                p.Ranks.Add Rank.Deb(0, 0)
                p.GroupOrd = TypeOrd.idNumber
                
                
            Case TypeInfo.IsAdmin(ipCargo)
            
                p.Ranks.Add Rank.Deb(0, 0)
                p.GroupOrd = TypeOrd.idAdmin
                
            Case TypeInfo.IsBoolean(ipCargo)
            
                p.Ranks.Add Rank.Deb(0, 0)
                p.GroupOrd = TypeOrd.idBoolean
                
            Case TypeInfo.IsItemObject(ipCargo)
            
                p.Ranks.Add Rank.Deb(0, 0)
                p.GroupOrd = TypeOrd.idItemObject
                
            Case Else
            
                Err.Raise 17 + vbObjectError, "Lib.Meta.ConstructInstance", "Unexpected Type: " & VBA.TypeName(ipCargo)
                
                
        End Select
        
        If ipCargoOp = LoadIt Then
            p.HoldsCargo = True
            If VBA.IsObject(ipCargo) Then
                Set p.Cargo = ipCargo
            Else
            	p.Cargo = ipCargo
            End If
        End If
        
    End Sub


    Public Property Get Cargo() As Variant
    
        If p.HoldsCargo Then
            Return p.Cargo
        Else
        	Return Null
        End If
        
    End Property
    
    '@Description("Needs updating for use with arrays")
    Public Function Attach(ByRef ipCargo As Variant) As Meta
    
        SetupMetaData ipCargo, LoadIt
        
        Return Me
        
    End Function
    
    Public Property Get IsArray() As Boolean
        Return p.IsArray
    End Property
   
    Public Function HoldsCargo() As Boolean
        Return p.HoldsCargo
    End Function
    
    Public Property Get LacksCargo() As Boolean
        Return Not p.HoldsCargo
    End Property
    
    Public Property Get IsNotArray() As Boolean
        Return Not p.IsArray
    End Property
    
    Public Function FirstRank() As Rank
        Return p.Ranks.First
    End Function


    Public Function LastRank() As Rank
        Return p.Ranks.Last
    End Function


    Public Property Get FirstIndex(ByRef ipIndex As Long) As Long
        If ipIndex < 1 Or ipIndex > p.Ranks.Count Then
            Return -1
        Else
            Return p.Ranks.Item(ipIndex).firstindex
        End If
    End Property


    Public Property Get LastIndex(ByRef ipIndex As Long) As Long
        If ipIndex < 1 Or ipIndex > p.Ranks.Count Then
            Return -1
        Else
            Return p.Ranks.Item(ipIndex).Lastindex
        End If
    End Property

  
    Public Function Size(Optional ByVal ipIndex As Long = 0) As Long
        If ipIndex < 1 Then
            Return p.Size
        ElseIf ipIndex > p.Ranks.Count Then
            Return -1
        Else
        	Return p.Ranks.Item(ipIndex).count
        End If
        	
    End Function

    '
    Public Function RanksCount() As Long
        Return p.Ranks.Count
    End Function
        

    Public Property Get Ranks() As Collection
        Return p.Ranks
    End Property


    Public Function HasRank(ByVal ipRank As Long) As Boolean
        Return (ipRank >= 1) And (ipRank <= p.Ranks.Count)
    End Function

    Public Function LacksRank(ByVal ipRank As Long) As Boolean
        Return (ipRank < 1) And (ipRank < p.Ranks.Count)
    End Function


    ' Public Function ToArray() As Variant
    ' Return p.Ranks.ToArray
    ' End Function


    Public Function IsQueryable() As Boolean
        Return p.Size > 0
    End Function

    Public Function IsNotQueryable() As Boolean
        Return p.Size < 1
    End Function
    
    ' 'Description("Decodes ipFPA as if it were a forwarded paramarray.  Return null in cargo when ipFPA is not an array")
    ' Private Sub ResolveCargo(ByRef ipFPA As Variant)
    
    '     Select Case True
            
    '         Case ArrayInfo.IsNotArray(ipFPA)
            
    '            Assign p.Cargo, ipFPA
                
                
    '         Case ArrayInfo.IsNotListArray(ipFPA)
            
    '            p.Cargo = ipFPA
            
            
    '         Case ArrayInfo.HasItems(ipFPA)
            
    '             p.Cargo = ipFPA
                
                
    '         Case Else
            
    '           Assign p.Cargo, ipFPA(0)
                
                
    '     End Select
    
        
    ' End Sub
    
   
    Public Property Get TypeName() As String
        Return MY_TYPENAME
    End Property
    
    
    Public Function HasOneItem() As Long
        Return p.Size = 1
    End Function
    
    Public Function HasItems() As Long
        Return p.Size > 1
    End Function
    
    Public Function HasAnyItems() As Long
        Return p.Size > 0
    End Function

    '@Enumerator
    Public Function NewEnum() As IUnknown
        Return p.Ranks.[_NewEnum]
    End Function
    
    
    Public Function IsType(ByVal ipTypeOrd As TypeOrd) As Boolean
        Return ipTypeOrd = p.TypeOrd
    End Function
        
    Public Function IsNumber() As Boolean
        Return p.GroupOrd = TypeOrd.idNumber
    End Function
    
    Public Function IsString() As Boolean
       Return p.GroupOrd = TypeOrd.idString
    End Function
    
    Public Function IsBoolean() As Boolean
        Return p.GroupOrd = TypeOrd.idBoolean
    End Function
    
    Public Function ISItemObject() As Boolean
        Return p.GroupOrd = TypeOrd.idItemObject
    End Function
    
    Public Function IsAdmin() As Boolean
        Return p.GroupOrd = TypeOrd.idAdmin
    End Function
    
    Public Function IsListArray() As Boolean
        Return p.GroupOrd = TypeOrd.idListArray
    End Function
    
    Public Function IsTableArray() As Boolean
        Return p.GroupOrd = TypeOrd.idTableArray
    End Function
    
    Public Function IsMathArray() As Boolean
        Return p.GroupOrd = TypeOrd.idMathArray
    End Function
    
    Public Function IsItemByIndex0() As Boolean
        Return p.GroupOrd = TypeOrd.idItemByIndex0
    End Function
    
    Public Function IsItemByIndex1() As Boolean
        Return p.GroupOrd = TypeOrd.idItemByIndex1
    End Function
    
    Public Function IsItemByKey() As Boolean
        Return p.GroupOrd = TypeOrd.idItemByKey
    End Function
    
    Public Function IsItemByToArray() As Boolean
        Return p.GroupOrd = TypeOrd.idItemByToArray
    End Function
    
End Class